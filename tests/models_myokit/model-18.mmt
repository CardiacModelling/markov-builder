[[model]]
name: model-18
author: Sanmitra Ghosh
desc: Check associated model definition document
# Initial values
ikr.C3 = 1
ikr.C2 = 0
ikr.C1 = 0
ikr.O = 0

#
# Simulation engine variables
#
[engine]
time = 0 bind time
pace = 0 bind pace

#
# Membrane potential
#
[membrane]
V = engine.pace
    desc: membrane potential
    in [mV]

[nernst]
EK = -85 [mV]

#
#
[ikr]
use membrane.V
IKr = p19 * O * (V - nernst.EK)
I = 1 - (O + C1 + C2 + C3)

a1 = p1 * exp( p2 * V)
b1 = p3 * exp(-p4 * V)
a2 = p5 * exp( p6 * V)
b2 = p7 * exp(-p8 * V)
a3 = p9 * exp(p10 * V)
b3 = p11 * exp(-p12 * V)
a4 = p13 * exp(p14 * V)
b4 = p15 * exp(-p16 * V)
a5 = p17 * exp(p18 * V)
b5 = (a5*b3*b4)/(a3*a4)

dot(C3) = +b1*C2 - a1*C3
dot(C2) = +b2*C1 + a1*C3 - C2*(a2 + b1)
dot(C1) = +b3*O  + a2*C2 + b5*I - C1*(a3 + b2 + a5)
dot(O)  = +b4*I  + a3*C1 - O*(b3 + a4)

p1 = 2.26e-4 [1/ms]
p2 = 0.06990 [1/mV]
p3 = 3.45e-5 [1/ms]
p4 = 0.05462 [1/mV]
p5 = 0.08730 [1/ms]
p6 = 8.91e-3 [1/mV]
p7 = 5.15e-3 [1/ms]
p8 = 0.03158 [1/mV]
p9 = 5.15e-3 [1/ms]
p10 = 0.03158 [1/mV]
p11 = 2.26e-4 [1/ms]
p12 = 0.06990 [1/mV]
p13 = 3.45e-5 [1/ms]
p14 = 0.05462 [1/mV]
p15 = 0.08730 [1/ms]
p16 = 8.91e-3 [1/mV]
p17 = 5.15e-3 [1/ms]
p18 = 0.03158 [1/mV]
p19 = 0.15240 [mS/uF]

n_params = 19
