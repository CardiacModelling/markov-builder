[[model]]
name: model-12
author: Sanmitra Ghosh
desc: Check associated model definition document
# Initial values
ikr.C2 = 1
ikr.C1 = 0
ikr.O = 0
ikr.I = 0
ikr.IC1 = 0

#
# Simulation engine variables
#
[engine]
time = 0 bind time
pace = 0 bind pace

#
# Membrane potential
#
[membrane]
V = engine.pace
    desc: membrane potential
    in [mV]

[nernst]
EK = -85 [mV]

# MODEL
#          2am       am
#     IC2 <==> IC1  <==>  I
#     ||   bm   ||   2bm || 
#  b3*||a3   b2*||a2   b1||a1
#     ||   2am  ||   am  || 
#     C2  <==>  C1  <==>  O
#          bm        2bm
#
[ikr]
use membrane.V
IKr = p13 * O * (V - nernst.EK)

IC2 = 1 - (C1 + C2 + O + I + IC1)

am = p1 * exp( p2 * V)
bm = p3 * exp(-p4 * V)
a1 = p5 * exp( p6 * V)
b1 = p7 * exp(-p8 * V)
a2 = p9 * exp( p10 * V)
a3 = p11 * exp(p12* V)

b2 = (a2*b1)/a1
b3 = (a3*b2)/a2

dot(C2) = b3*IC2 + bm*C1 - C2*(a3 + 2*am)
dot(C1) = b2*IC1 + 2*bm*O + 2*am*C2 -C1*(am + bm + a2)
dot(O) = b1*I + am*C1 -O*(a1+2*bm)
dot(I) = a1*O + am* IC1 - I*(b1 + 2*bm)
dot(IC1) = 2*bm*I + 2*am*IC2 + a2*C1 - IC1*(am + bm + b2)


p1 = 2.26e-4 [1/ms]
p2 = 0.06990 [1/mV]
p3 = 3.45e-5 [1/ms]
p4 = 0.05462 [1/mV]
p5 = 0.08730 [1/ms]
p6 = 8.91e-3 [1/mV]
p7 = 5.15e-3 [1/ms]
p8 = 0.03158 [1/mV]
p9 = 2.26e-4 [1/ms]
p10 = 0.06990 [1/mV]
p11 = 3.45e-5 [1/ms]
p12 = 0.05462 [1/mV]
p13 = 0.15240 [mS/uF]

n_params = 13

