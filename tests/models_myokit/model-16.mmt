[[model]]
name: model-16
author: Sanmitra Ghosh
desc: Check associated model definition document
# Initial values
ikr.C3 = 1
ikr.C2 = 0
ikr.C1 = 0
ikr.O = 0

#
# Simulation engine variables
#
[engine]
time = 0 bind time
pace = 0 bind pace

#
# Membrane potential
#
[membrane]
V = engine.pace
    desc: membrane potential
    in [mV]

[nernst]
EK = -85 [mV]

#
# Wang et al. 1997 Model from Kylie's mex file (HH)
#
[ikr]
use membrane.V
IKr = p15 * O * (V - nernst.EK)
I = 1 - (O + C1 + C2 + C3)
a1 = p1 * exp( p2 * V)
b1 = p3 * exp(-p4 * V)
a3 = p5 * exp( p6 * V)
b3 = p7 * exp(-p8 * V)
a4 = p9 * exp( p10 * V)
b4 = p11 * exp(-p12 * V)
a2 = p13 
b2 = p14 

dot(C1) = b3  * O + a2 * C2 -(a3 + b2) * C1
dot(C2) = b2 * C1 + a1 * C3 - (a2 +b1) * C2
dot(C3) = b1 * C2 - a1 * C3
dot(O) = b4 * I + a3 * C1 - (a4 + b3) * O

p1 = 2.26e-4 [1/ms]
p2 = 0.06990 [1/mV]
p3 = 3.45e-5 [1/ms]
p4 = 0.05462 [1/mV]
p5 = 0.08730 [1/ms]
p6 = 8.91e-3 [1/mV]
p7 = 5.15e-3 [1/ms]
p8 = 0.03158 [1/mV]
p9 = 0.08730 [1/ms]
p10 = 8.91e-3 [1/mV]
p11 = 5.15e-3 [1/ms]
p12 = 0.03158 [1/mV]
p13 = 0.08730 [1/ms]
p14 = 8.91e-3 [1/mV]
p15 = 0.15240 [mS/uF]

n_params = 15
