[[model]]
name: model-8
author: Sanmitra Ghosh
desc: Check associated model definition document
# Initial values
ikr.y1 = 1
ikr.y2 = 0
ikr.O = 0

#
# Simulation engine variables
#
[engine]
time = 0 bind time
pace = 0 bind pace

#
# Membrane potential
#
[membrane]
V = engine.pace
    desc: membrane potential
    in [mV]

[nernst]
EK = -85 [mV]

#
# Model from Kylie's mex file (HH)
#
[ikr]
use membrane.V
IKr = p13 * O * (V - nernst.EK)
y4 = 1 - (y1 + y2 + O)
k12 = p1 * exp( p2 * V)
k21 = p3 * exp(-p4 * V)
k34 = p5 * exp( p6 * V)
k43 = p7 * exp(-p8 * V)
k56 = p9 * exp( p10 * V)
k65 = p11 * exp(-p12 * V)

dot(y1) = -k12 * y1 + k21 * y2
dot(y2) = -(k21 + k34) * y2 + k12 * y1 + k43 * O
dot(O) = -(k43 + k56) * O + k34 * y2 + k65 * y4

p1 = 2.26e-4 [1/ms]
p2 = 0.06990 [1/mV]
p3 = 3.45e-5 [1/ms]
p4 = 0.05462 [1/mV]
p5 = 0.08730 [1/ms]
p6 = 8.91e-3 [1/mV]
p7 = 5.15e-3 [1/ms]
p8 = 0.03158 [1/mV]
p9 = 0.08730 [1/ms]
p10 = 8.91e-3 [1/mV]
p11 = 5.15e-3 [1/ms]
p12 = 0.03158 [1/mV]
p13 = 0.15240 [mS/uF]


n_params = 13
