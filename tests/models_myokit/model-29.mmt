[[model]]
name: model-29
author: Sanmitra Ghosh
desc: Check associated model definition document
# Initial values
ikr.C4 = 1
ikr.C3 = 0
ikr.C2 = 0
ikr.C1 = 0
ikr.O = 0
ikr.I = 0
ikr.IC1 = 0
ikr.IC2 = 0
ikr.IC3 = 0

#
# Simulation engine variables
#
[engine]
time = 0 bind time
pace = 0 bind pace

#
# Membrane potential
#
[membrane]
V = engine.pace
    desc: membrane potential
    in [mV]

[nernst]
EK = -85 [mV]

#
# Model from Kylie's mex file (HH)
#
[ikr]
use membrane.V
IKr = p17 * O * (V - nernst.EK)

IC4 = 1 - (C1 + C2 + C3 + C4 + O + I + IC1 + IC2 + IC3)

am = p1 * exp( p2 * V)
bm = p3 * exp(-p4 * V)
a1 = p5 * exp( p6 * V)
b1 = p7 * exp(-p8 * V)
a2 = p9 * exp( p10 * V)
b2 = (a2*b1)/a1
a3 = p11 * exp( p12* V)
b3 = (a3*b2)/a2
a4 = p13 * exp( p14 * V)
b4 = (a4*b3)/a3
a5 = p15 * exp( p16 * V)
b5 = (a5*b4)/a4

dot(C4) = bm*C3 + b5*IC4 - C4*(4*am+a5) 
dot(C3) = 2*bm*C2 + 4*am*C4 + b4*IC3 - C3*(a4 + 3*am - bm)
dot(C2) = 3*bm*C1 + 3*am*C3 + b3*IC2 - C2*(a3 + 2*am + 2*bm)
dot(C1) = 4*bm*O + 2*am*C2 + b2*IC1 - C1*(am + 3*bm + a2)
dot(O) = b1*I + am*C1 - O*(4*bm + a1)
dot(I) = a1*O + am*IC1 - I*(b1 + 4*bm)
dot(IC1) = 4*bm*I + 2*am*IC2 + a2*C1 - IC1*(3*bm + am + b2)
dot(IC2) = 3*bm*IC1 + 3*am*IC3 + a3*C2 - IC2*(2*am + b3 + 2*bm)
dot(IC3) = 2*bm*IC2 + 4*am*IC4 + a4*C3 - IC3*(3*am + b4 + bm)

p1 = 2.26e-4 [1/ms]
p2 = 0.06990 [1/mV]
p3 = 3.45e-5 [1/ms]
p4 = 0.05462 [1/mV]
p5 = 0.08730 [1/ms]
p6 = 8.91e-3 [1/mV]
p7 = 5.15e-3 [1/ms]
p8 = 0.03158 [1/mV]
p9 = 2.26e-4 [1/ms]
p10 = 0.06990 [1/mV]
p11 = 3.45e-5 [1/ms]
p12 = 0.05462 [1/mV]
p13 = 0.08730 [1/ms]
p14 = 8.91e-3 [1/mV]
p15 = 0.08730 [1/ms]
p16 = 8.91e-3 [1/mV]
p17 = 0.15240 [mS/uF]

n_params = 17

